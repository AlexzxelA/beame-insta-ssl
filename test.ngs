#!/usr/bin/env ngs

assert('BEAME_INTERNAL_AUTH_SERVER_FQDN' in ENV, 'Environment variable BEAME_INTERNAL_AUTH_SERVER_FQDN must be set')

HOME = ENV.HOME

BEAME_DIR = HOME / '.beame'

if Path(BEAME_DIR) {
	throw Error("Beame directory exists ($BEAME_DIR). Will not run tests.")
}

BEAME_INSTA_SSL_BIN = HOME / 'beame-insta-ssl/main.js'
BEAME_SDK_BIN = HOME / 'beame-sdk/src/cli/beame.js'
BEAME_DEV_AUTH_FQDN = 'n6ge8i9q4b4b5vb6.h40d7vrwir2oxlnn.v1.d.beameio.net'
EXPECTED_EXPORTED_FILE_EXTENSIONS = %[pem key chain.p7b pkcs12 pkcs12.pwd]

# ---------- Utilities ----------

F base64encode(s:Str) {
	fname = "/tmp/base64.${c_getpid()}"
	$(echo $s >$fname)
	ret = `base64 -w0 <$fname`
	$(rm $fname)
	log("base64 done")
	ret
}

F base64decode(s:Str) {
	fname = "/tmp/base64decode.${c_getpid()}"
	$(echo $s >$fname)
	ret = `base64 -d <$fname`
	$(rm $fname)
	log("base64 decode done")
	ret
}

F fqdn_dir(fqdn) "$HOME/.beame/v2/$fqdn"

# ---------- Tests code ----------


doc Copies Beame test requests signing credential into store for L0 entity creation
F copy_to_store() {
	$(cp -a "$HOME/$BEAME_DEV_AUTH_FQDN" ${fqdn_dir(BEAME_DEV_AUTH_FQDN)})
	TestMessage('Copied')
}

doc Creates token needed for signing request for level 0 entity
F create_token() {
	global token = `$BEAME_SDK_BIN token create --fqdn $BEAME_DEV_AUTH_FQDN --data NONE`
	assert(token is Str, "Token expected to be a string")
	TestMessage(token)
}

doc Creates level 0 entity
F create_L0() {
	t = time()
	beame_insta_ssl_token = {
		'authToken': token.base64decode().parse()
		'authSrvFqdn': ENV.BEAME_INTERNAL_AUTH_SERVER_FQDN
		'name': "insta-ssl-test-L0-$t"
		'email': "insta-ssl-L0-${t}@example.com"
	}
	txt = `$BEAME_INSTA_SSL_BIN create ${beame_insta_ssl_token.encode_json().base64encode()}`
	cert_created_line = txt.lines().filter(X ~ Pfx('Certificate created'))[0]
	global fqdn = cert_created_line.split(' ')[-1]
	TestMessage("Entity $fqdn created")
}

doc Removes Beame test requests signing credential from store
F remove_from_store() {
	$(rm -r ${fqdn_dir(BEAME_DEV_AUTH_FQDN)})
	TestMessage('Removed')
}

doc Checks that process outputs specific text and exists with non-zero code
F should_fail_with_text(cmd:Command, text:Str) {
	try {
		$($cmd)
		throw TestFail('Exited with code 0')
	} catch(e:ProcessFail) {
		text not in e.process.stdout throws TestFail("No expected text in output")
		return 'Expected text found'
	}
}

doc Tests non-terminating tunnel
F https_target_tunnel(cmd:Command) {
	t = time().Str()
	threads = [
		Thread({ $(node testHttpsServer.js $fqdn $t) })
		Thread({ $($cmd) })
	]
	$(sleep 10)
	out = try `curl --silent --max-time 30 "https://$fqdn/"`
	$(pkill -f 'testHttpsServer.js')
	$(pkill -f 'tunnel 65500')
	threads.join()
	out != t throws TestFail("Result from server is missing or incorrect: '$out' vs '$t'")
	TestMessage('Result from server is OK')
}

doc Tests terminating tunnel
F http_target_tunnel(cmd:Command, expected_hostname:Str, proto='https') {
	t = time().Str()
	threads = [
		Thread({ $(node testHttpServer.js $t) })
		Thread({ $($cmd) })
	]
	$(sleep 10)
	out = try `curl --silent --max-time 30 "$proto://$fqdn/"`
	$(pkill -f 'testHttpServer.js')
	$(pkill -f 'tunnel 65500')
	threads.join()
	out != "$t-$expected_hostname" throws TestFail("Result from server is missing or incorrect: '$out' vs '$t-$expected_hostname'")
	TestMessage('Result from server is OK')
}

doc Waits till hostname becomes resolvable (has DNS record)
F wait_resolvable(h:Str) {
	test("Waiting for hostname $h to be resolvable", {
		for(i;45) {
			resolve = `dig "+short" $h`
			resolve returns "Resolves to ${resolve.lines()[-1]}"
			$(sleep 2)
		}
		throw TestFail("No resolve for $h")
	})
}

F corrupt_metadata_json() {
	metadata = ``cat "${fqdn_dir(fqdn)}/metadata.json"``
	metadata.edge_fqdn = 'corrupted'
	$(echo ${metadata.encode_json()} >"${fqdn_dir(fqdn)}/metadata.json")
	metadata_json_ok(fqdn) throws TestFail("Metadata JSON is OK while should be corrupted")
	TestMessage("Metadata file corrupted")
}

F metadata_json_ok(fqdn) {
	metadata = ``cat "${fqdn_dir(fqdn)}/metadata.json"``
	metadata.edge_fqdn != 'corrupted'
}

# ---------- No certificates test ----------

test("Run without certificates", {
	should_fail_with_text(%($BEAME_INSTA_SSL_BIN list), 'you have no certificates')
})

# ---------- One certificate test ----------

test("Copy $BEAME_DEV_AUTH_FQDN into store", copy_to_store)
test("Create token for test L0 entity", create_token)
test("Create test L0 entity", create_L0);
test("Remove $BEAME_DEV_AUTH_FQDN from store", remove_from_store)

test("List certificates - one certificate", {
	certs = `$BEAME_INSTA_SSL_BIN list`.lines()
	assert(certs == [fqdn])
	TestMessage('List is OK')
})

test("Corrupt metadata JSON", corrupt_metadata_json)

test("Update metadata", {
	$($BEAME_INSTA_SSL_BIN syncmeta)
})

test("Metadata OK", {
	metadata_json_ok(fqdn).not() throws TestFail("Metadata JSON is not OK")
})

test("Export certificate", {
	dir = `mktemp --tmpdir -d test-beame-insta-ssl.XXXXXXXXXX` - MaybeSfx('\n')
	err = null
	ret = null
	try {
		$($BEAME_INSTA_SSL_BIN export $fqdn $dir)
		EXPECTED_EXPORTED_FILE_EXTENSIONS.each(F(ext) {
			f = "$dir/$fqdn.$ext"
			not($(test -s $f)) throws TestFail("File $f not found")
		})
	} catch(e) {
		err = e
	}
	$(rm -r $dir)
	err throws err
	TestMessage('All expected files are present')
})

wait_resolvable(fqdn)

test("eehttp tunnel without fqdn", {
	http_target_tunnel(%($BEAME_INSTA_SSL_BIN tunnel 65500 eehttp), fqdn, 'http')
})

test("Non-terminating tunnel without fqdn", {
	https_target_tunnel(%($BEAME_INSTA_SSL_BIN tunnel 65500 https))
})

test("Terminating tunnel without fqdn", {
	http_target_tunnel(%($BEAME_INSTA_SSL_BIN tunnel 65500 http), 'localhost')
})

# ---------- 2 or more certificates test ----------

old_fqdn = fqdn
test("Copy $BEAME_DEV_AUTH_FQDN into store", copy_to_store)
test("Create token for additional test L0 entity", create_token)
test("Create additional test L0 entity", create_L0)
test("Remove $BEAME_DEV_AUTH_FQDN from store", remove_from_store)

test("Corrupt metadata JSON", corrupt_metadata_json)

test("Update metadata", {
	$($BEAME_INSTA_SSL_BIN syncmeta --fqdn $fqdn)
})

test("Metadata OK", {
	metadata_json_ok(fqdn).not() throws TestFail("Metadata JSON is not OK")
})


test("List certificates - two certificates", {
	certs = `$BEAME_INSTA_SSL_BIN list`.lines()
	assert(certs.sort() == [old_fqdn, fqdn].sort())
})


test("Run tunnel not specifying fqdn", {
	should_fail_with_text(%($BEAME_INSTA_SSL_BIN tunnel 65500 https), 'more than one')
})

wait_resolvable(fqdn)

test("Non-terminating tunnel with fqdn", {
	https_target_tunnel(%($BEAME_INSTA_SSL_BIN tunnel 65500 https --fqdn $fqdn))
})

test("Terminating tunnel with fqdn", {
	http_target_tunnel(%($BEAME_INSTA_SSL_BIN tunnel 65500 http --fqdn $fqdn), 'localhost')
})

test("Terminating tunnel with fqdn and hostname", {
	http_target_tunnel(%($BEAME_INSTA_SSL_BIN tunnel 65500 http --fqdn $fqdn --hostname test1.example.com), 'test1.example.com')
})
